# -*- coding: utf-8 -*-

# Finnish word Estonification script

# Copyright (C) 2017 Kristian Kankainen
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.

# This script generates a transducer that makes Finnish words look and
# feel more like their Estonian counterparts.
# Most of the rules are taken from the paper Kasik, Reet. 1997. Hakkame
# rääkima!: viron kielen peruskurssi. (Publications of the Department of
# Finnish and General Linguistics of the University of Turku 37). Turku.
# This script is done for educational purposes of the author.


! Use weights
set encode-weights ON
set print-weight ON

define C [p|f|g|c|r|l|d|h|t|n|s|q|j|k|x|b|m|w|v|z];
define VoicedC [j|l|m|n|r|v];
define V [a|o|e|u|i|ä|{õ}|ö|ü|y];
define BackV [o|a|u];
define FrontV [V - BackV];

define ShortS [C V];
define LongOS [C V V];
define LongCS [C V V C];



!! Vowel rules
! Finnish y is written as ü in Estonian (vowel rule number 2)
define VRule2 [y -> ü];

! First syllables are not diphtongized in Estonian (vowel rule nr 7).
define VRule7 [{uo} -> {oo}
              ,{üö} -> {öö}
              || .#. C* _];
! The diphtong ie can be fronted or backed
define VRule7a [{ie} -> {ee} || .#. C* _ $[FrontV]];
define VRule7b [{ie} -> {õõ} || .#. C* _ $[BackV]];
define VRule7c [{ie} -> {ee} || .#. C* _ ~$[FrontV|BackV]]; ! default

! Other probable diphtong correspondencies (vowel rule 8).
define VRule8 [{ai} -> {ae}
              ,{oi} -> [{oe}|{õi}]
              ,{ou} -> {õu}
              ,{äü} -> {äi}
              ,{öü} -> [{ei}|{öi}]
              || .#. C* _];

! Apocope (vowel rule number 3)
! the last vowel is dropped after a long syllable
define VRule3 [V -> 0
              || C C _ .#.
              , V V C _ .#.];

! Word-final j is written as i
define VRule3a [j -> i || _ .#.];

! Vowel correspondencies in non-initial syllables (vowel rule nr 4).
define VRule4 [o -> u
              ,ö -> u
              ,ä -> [a|e]
              ,ü -> [i|u]
              || .#. [C* V+ C+]+ _];

! Syncope (vowel rule nr 6).
! @todo should the context apply word initially or word finally?
! @todo does syncope occur only if ending in 'er' or 'el'?
! @todo add weight when rule is not applied
define VRule6 [V+ (->) 0
              || C V V C C+ _ C V
              ,  C V C C+ _ C V
              ,  C V V C _ C V];



!! Consonant rules
! Long consonants are written with one grapheme if after long vowel or diphtong
! or in a consonant cluster. (consonant rule number 3.)
! @todo does this apply to all consonants?
define CRule3 [{kk} -> k
              ,{pp} -> p
              ,{tt} -> t
              || V V _
              ,  C _
              ,  _ C];


! Also shorten long consonants in the end of the word
! @todo this should apply to all consonants
define CRule3a [{kk} -> k
               ,{pp} -> p
               ,{tt} -> t
               || _ .#.];


! Finnish kpt is written as gbd if between vowels or after a voiced consonant.
! (consonant rule number 1.)
! @todo what about word-final kpt?
define CRule1 [k -> g
              ,p -> b
              ,t -> d
              || V _ V
              ,  V _ .#.
              ,  VoicedC _ ?];

! Word-final n is dropped everywhere except in verbs. (consonant rule number 5.)
define CRule5 [n (->) 0 || _ .#.];
! if n is not dropped, the word is a verb, but this is rare so we add a penalty
define CRule5a [[. .] -> ["+V"::1] || n _ .#.];

! Word-internal d is dropped in Estonian (consonant rule nr 7).
! @todo should we add a syllable border here?
define CRule7 [d -> 0 || V _ V, h _ V];

! long vowels are shortened before dropping the d
define CRule7a [{aa} -> a
              ,{oo} -> o
              ,{ee} -> e
              ,{uu} -> u
              ,{ii} -> i
              ,{ää} -> ä
              ,{õõ} -> {õ}
              ,{öö} -> ö
              ,{üü} -> ü
              ,{yy} -> y
              || _ d V];

! @todo add consonant rule nr 2
! @todo add other "phonetical" rules
! @todo add syllabification rules

! Long vowels are shortened in front of h (vowel rule 9).
define CRule9 [{aa} -> a
              ,{oo} -> o
              ,{ee} -> e
              ,{uu} -> u
              ,{ii} -> i
              ,{ää} -> ä
              ,{õõ} -> {õ}
              ,{öö} -> ö
              ,{üü} -> ü
              ,{yy} -> y
              || _ h];

! h disappears if in beginning of a syllable (consonant rule nr 8).
define CRule8 [h -> 0 \\ C _ V];



! @todo should not all long consonants be shortened if word-final?

! order and compose the rules
regex [
  CRule8 ! h disappears if in beginning of a syllable
  .o.  VRule2  ! Finnish y is written as ü in Estonian
  .o.  VRule7  ! First syllables are not diphtongized in Estonian
  .o.  VRule7a ! The diphtong ie can be fronted
  .o.  VRule7b ! The diphtong ie can be backed
  .o.  VRule7c ! The diphtong ie is fronted by default
  .o.  VRule3  ! Apocope e.g the last vowel is dropped after a long syllable
  .o.  VRule3a ! Word-final j is written as i
  .o.  VRule4  ! Vowel correspondencies in non-initial syllables
  .o.  VRule6  ! Syncope
  .o.  CRule7a ! long vowels are shortened before dropping the d
  .o.  CRule7  ! Word-internal d is dropped
  .o.  CRule3  ! Long consonants are written with one grapheme
  .o.  CRule3a ! Long consonants are shorted in the end of the word
  .o.  CRule1  ! Finnish kpt is written as gbd
  .o.  CRule5  ! Word-final n is dropped
  .o.  CRule5a ! if word-final n is not dropped, the word is a verb
  .o.  VRule8  ! Other probable diphtong correspondencies
  .o.  CRule9  ! Long vowels are shortened in front of h
];
