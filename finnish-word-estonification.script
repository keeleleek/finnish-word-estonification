# -*- coding: utf-8 -*-

# Finnish word Estonification script

# Copyright (C) 2017 Kristian Kankainen
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.

# This script generates a transducer that makes Finnish words look and
# feel more like their Estonian counterparts.
# Most of the rules are taken from the paper Kasik, Reet. 1997. Hakkame
# rääkima!: viron kielen peruskurssi. (Publications of the Department of
# Finnish and General Linguistics of the University of Turku 37). Turku.
# This script is done for educational purposes of the author.


! Use weights
set encode-weights ON
set print-weight ON

define C [p|f|g|c|r|l|d|h|t|n|s|q|j|k|x|b|m|w|v|z];
define VoicedC [j|l|m|n|r|v];
define V [a|o|e|u|i|ä|{õ}|ö|ü|y];
define BackV [o|a|u];
define FrontV [V - BackV];


define ShortS [C V];
define LongOS [C V V];
define LongCS [C V V C];

!! Vowel rules
! Finnish y is written as ü in Estonian (vowel rule number 2)
regex [y -> ü];

! First syllables are not diphtongized in Estonian (vowel rule nr 7).
regex [{uo} -> {oo}
      ,{üö} -> {öö}
      || .#. C* _];
! The diphtong ie can be fronted or backed
regex [{ie} -> {ee} || .#. C* _ $[FrontV]];
regex [{ie} -> {õõ} || .#. C* _ $[BackV]];
regex [{ie} -> {ee} || .#. C* _ ~$[FrontV|BackV]]; ! default

! Other probable diphtong correspondencies (vowel rule 8).
regex [{ai} -> {ae}
      ,{oi} -> [{oe}|{õi}]
      ,{ou} -> {õu}
      ,{äü} -> {äi}
      ,{öü} -> [{ei}|{öi}]
      || .#. C* _];

! Apocope (vowel rule number 3)
! the last vowel is dropped after a long syllable
regex [V -> 0 || C C _ .#.
               , V V C _ .#.];

! Word-final j is written as i
regex [j -> i || _ .#.];

! Vowels in non-initial syllables (vowel rule nr 4).
regex [o -> u
      ,ö -> u
      ,ä -> [a|e]
      ,ü -> [i|u]
      || .#. [C* V+ C+]+ _];

! Syncope (vowel rule nr 6).
regex [V+ -> 0 || .#. C V V C C _ C V
                , .#. C V C C _ C V
                , .#. C V V C _ C V];


!! Consonant rules
! Long consonants are written with one grapheme if after long vowel or diphtong
! or in a consonant cluster. (consonant rule number 3.)
regex [{kk} -> k
      ,{pp} -> p
      ,{tt} -> t
      || V V _
      ,  C _
      ,  _ C];

! Finnish kpt is written as gbd if between vowels or after a voiced consonant.
! (consonant rule number 1.)
regex [k -> g
      ,p -> b
      ,t -> d
      || V _ V
      ,  V _ .#.
      ,  VoicedC _ ?];

! Word-final n is dropped everywhere excep in verbs. (consonant rule number 5.)
regex [n (->) 0 || _ .#.];
! if n is not dropped, the word is a verb, but this is rare so add a penalty
regex [[. .] -> ["+V"::1] || n _ .#.];

! Word-internal d is dropped in Estonian (consonant rule nr 7).
regex [d -> 0 || V _ V, h _ V];

! @todo add consonant rule nr 2
! @todo add other "phonetical" rules
! @todo add syllabification rules

! Long vowels are shortened in front of h (vowel rule 9).
regex [{aa} -> a
      ,{oo} -> o
      ,{ee} -> e
      ,{uu} -> u
      ,{ii} -> i
      ,{ää} -> ä
      ,{õõ} -> {õ}
      ,{öö} -> ö
      ,{üü} -> ü
      ,{yy} -> y
      || _ h];

! h disappears if in beginning of a syllable (vowel rule nr 8).
regex [h -> 0 \\ C _ V];



rotate stack ! this makes the last rule the first to apply
compose net
