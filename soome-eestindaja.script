! kasuta kaalusid
set encode-weights ON
set print-weight ON

define C [p|f|g|c|r|l|d|h|t|n|s|q|j|k|x|b|m|w|v|z];
define VoicedC [j|l|m|n|r|v];
define V [a|o|e|u|i|ä|{õ}|ö|ü|y];
define BackV [o|a|u];
define FrontV [V - BackV];


define ShortS [C V];
define LongOS [C V V];
define LongCS [C V V C];

!! Vowel rules
! Finnish y is written as ü in Estonian (vowel rule number 2)
regex [y -> ü];

! First syllables are not diphtongized in Estonian (vowel rule nr 7).
regex [{uo} -> {oo}
      ,{üö} -> {öö}
      || .#. C* _];
! The diphtong ie can be fronted or backed
regex [{ie} -> {ee} || .#. C* _ $[FrontV]];
regex [{ie} -> {õõ} || .#. C* _ $[BackV]];

! Other probable diphtong correspondencies (vowel rule 8).
regex [{ai} -> {ae}
      ,{oi} -> [{oe}|{õi}]
      ,{ou} -> {õu}
      ,{äü} -> {äi}
      ,{öü} -> [{ei}|{öi}]
      || .#. C* _];

! Apocope (vowel rule number 3)
! the last vowel is dropped after a long syllable
regex [V -> 0 || C C _ .#.
               , V V C _ .#.];

! Word-final j is written as i
regex [j -> i || _ .#.];

! 4. jälkitavun vokaalit
regex [o -> u
      ,ö -> u
      ,ä -> [a|e]
      ,ü -> [i|u]
      || .#. [C* V+ C+]+ _];

! Syncope (vowel rule nr 6.)
regex [V+ -> 0 || .#. C V V C C _ C V
                , .#. C V C C _ C V
                , .#. C V V C _ C V];


!! Consonant rules
! Long consonants are written with one grapheme if after long vowel or diphtong
! or in a consonant cluster. (consonant rule number 3.)
regex [{kk} -> k
      ,{pp} -> p
      ,{tt} -> t
      || V V _
      ,  C _
      ,  _ C];

! Finnish kpt is written as gbd if between vowels or after a voiced consonant.
! (consonant rule number 1.)
regex [k -> g
      ,p -> b
      ,t -> d
      || V _ V
      ,  V _ .#.
      ,  VoicedC _ ?];

! Word-final n is dropped everywhere excep in verbs. (consonant rule number 5.)
regex [n (->) 0 || _ .#.];
! if n is not dropped, the word is a verb, but this is rare so add a penalty
regex [[. .] -> ["+V"::1] || n _ .#.];

! Word-internal d is dropped in Estonian (consonant rule nr 7).
regex [d -> 0 || V _ V, h _ V];

! @todo add consonant rule nr 2
! @todo add other "phonetical" rules
! @todo add syllabification rules

! Long vowels are shortened in front of h (vowel rule 9).
regex [{aa} -> a
      ,{oo} -> o
      ,{ee} -> e
      ,{uu} -> u
      ,{ii} -> i
      ,{ää} -> ä
      ,{õõ} -> {õ}
      ,{öö} -> ö
      ,{üü} -> ü
      ,{yy} -> y
      || _ h];

! 8. h disappears if in beginning of a syllable
regex [h -> 0 \\ C _ V];



rotate stack ! this makes the last rule the first to apply
compose net
